//load_file("nrngui.hoc") 
//load_proc("nrnmainmenu")

// ************************** Model specification *******************************
proc params() {
	// Geometrical properties
	num_nodes = 51					// number of nodes [unitless]
	node_diam = 8					// node diameter [um]
	node_length = 1					// node length [um]
	myelin_length = 100*node_diam	// internodal length [um]
	
	// Electrical properties
	node_cm = 2					// specific membrane capacitance [uF/cm^2]
	rhoa = 200					// intracellular resistivity [ohm-cm]
	rhoe = 1000					// extracellular resistivity [ohm-cm]
	node_Rm = 1500				// specific membrane resistance [ohm-cm^2]
	ap_thresh = 0 				// action potential threshold
	
	// Stimulus parameters
	mydel = 5					// start at t=5ms [ms]
	myamp = -1.0 				// amplitude [nA]
	mydur = 0.1					// duration, aka pulsewidth [ms]
	
	// Temporal parameters
	dt = 0.001					// [ms]
	tstop = 10					// [ms]
	num_timesteps = int(tstop/dt) + 1
	
	// Other parameters
	v_init = -70				// [mV]
	celsius = 6.3				// [deg C]
}
params()

func round(){
	if ($1>0){
		return int($1+0.5)
	} else {
		return int($1-0.5)
	}
}

// ************************** Model initialization ******************************
create axon[num_nodes]
proc initialize() {local i
	for i = 0, num_nodes - 1 {
		axon[i] {
			nseg 	= 1
			diam 	= node_diam
			L 		= node_length
			Ra 		= rhoa * ((node_length+myelin_length)/node_length)
			cm		= node_cm
			
			// Insert passive channel
			insert hh
			insert extracellular
			g_hh = 1/node_Rm // do we need to change this from g_pas -> g_hh?
			
		}
	}
	
	for i = 0, num_nodes - 2 {
		connect axon[i](1), axon[i+1](0)
	}
}
initialize()

// ************************** Instrumentation ***********************************
// Extracellular stimulation
objref stim
create dummy
dummy {
	stim = new IClamp()
	stim.loc(0.5)
	stim.del = mydel	// [ms]
	stim.amp = myamp	// [nA]
	stim.dur = mydur 	// [ms]
}

// Record Vm(t) at all nodes
objref Vm_vec[num_nodes], Istim_vec
Istim_vec = new Vector(tstop/dt)
for i = 0, num_nodes - 1 {
	Vm_vec[i] = new Vector(num_timesteps,0)
	Vm_vec[i].record(&axon[i].v(0.5),dt)
}

// add APCount object to node 20
objref apc
axon[19] apc = new APCount(0.5)

// distance from electrode to each node
objref r_axon,xdist,ydist
r_axon = new Vector(num_nodes,0)
xdist  = new Vector(num_nodes,0) 
ydist  = new Vector(num_nodes,0.1) // 1 mm away

for i=0,num_nodes-1{
	xdist.x[i] = 0.5 + abs(i-round(num_nodes/2))*(myelin_length+node_length/2)
}

xdist.pow(2)
ydist.pow(2)

for i=0,num_nodes-1{
	r_axon.x[i] = sqrt(xdist.x[i]+ydist.x[i])
}

// ************************** Simulation control ********************************
proc stimul() {
	finitialize(v_init)
	while(t<tstop) {
		for i=0,num_nodes-1{
			axon[i].e_extracellular = rhoe * stim.i/(4*PI*r_axon.x[i])
		}
		fadvance()
	}
}
stimul()

print "Check stimMax: apc.n = ",apc.n


// ************************** Binary Search Algo ********************************
stimMax = -1
stimMin = 0

func cutoff(){
	return int($1*1000)/1000
}

proc iterative(){
	count=0
	while (count < 11) {
		thisStim = cutoff((stimMax-stimMin)/2+stimMin)
		stim.amp = thisStim
		stimul()

		if (apc.n >= 1){
			print "(apc.n >= 1) --> stimMax: ",stimMax,"to ",thisStim
			stimMax = thisStim
		} else if (apc.n < 1){
			print "(apc.n < 1) --> stimMin: ",stimMin,"to ",thisStim
			if (stimMin == thisStim) { print "Stimulus threshold = ",thisStim-0.001,"nA" }
			stimMin = thisStim
		}
		count+=1
		print "counter: ",count
	}
}
iterative()

// check that final choice is superthreshold
myamp = thisStim-0.001
stimul()
print "Final Check for stim=",myamp," apc.n=",apc.n